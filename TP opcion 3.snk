[Subsistema general]
; -- Simulación - Trabajo  Práctico Grupo 10 | Tema: Venta de Piezas de Bronce --
;---- PREPARACION DEL PEDIDO ---------------------------------
PREPARAR	TABULATE	QSINT1
		QUEUE		PREP		; PREPARAR EL PEDIDO 
		ENTER		PREP
		DEPART	PREP
		MARK		2
		ADVANCE	300,120	; 5+-2 MINUTOS
		ASSIGN	PRODAUX, P$PRODUCTOS
OTPROD3	ADVANCE	5,2		; 5+-2 POR PRODUCTO
		LOOP		PRODAUX, OTPROD3
		ASSIGN	Z, V$LOTEPIEZ	
OTPIEZ	ADVANCE	3,1		; DE A 20 PIEZAS
		LOOP		Z, OTPIEZ
		LEAVE		PREP
		TABULATE	TPREP
;---- FACTURACION DEL PEDIDO ---------------------------------
FACTPED	QUEUE		FACT		; FACTURAR EL PEDIDO
		SEIZE		FACT
		DEPART	FACT
		ADVANCE	120,30
		ASSIGN	PRODAUX,P$PRODUCTOS
OTPROD4	ADVANCE	5,2
		LOOP		PRODAUX,OTPROD4
		RELEASE	FACT
		TABULATE	QPEDTERM
		TERMINATE			; EL CLIENTE SE VA.


[Subsistema simular 10hs]
; -- Simulación - Trabajo  Práctico Grupo 10 | Tema: Venta de Piezas de Bronce --
		GENERATE	3600		; SIMULAR 1 DÍA, DE 8 A 18HS.
		TERMINATE	1
;		START		10		


[Subsistema Telefónico]
; -- Simulación - Trabajo  Práctico Grupo 10 | Tema: Venta de Piezas de Bronce --
; -- PEDIDOS TELEFONICOS --
		GENERATE	1200, 600	; LLAMADAS cada 20 +/- 10 minutos.
		QUEUE		TEL
		SEIZE		TEL
		DEPART	TEL
		ADVANCE	180,60	; 3+-1 MINUTOS GENERAL
		ASSIGN	PRODUCTOS, FN$CANTPRODTEL	; OBTENGO CANTIDAD DE PRODUCTOS AL AZAR
		ASSIGN	CANTPIEZ, FN$CANTPIEZ		; OBTENGO CANTIDAD DE PIEZAS AL AZAR
		ASSIGN	PRODAUX, P$PRODUCTOS			; AUXILIAR PARA LOOP
OTPROD2	ADVANCE	7,2		; 7+-2SEG POR PRODUCTO
		LOOP		PRODAUX, OTPROD2
		RELEASE	TEL
		TRANSFER	,PREPARAR	; IR A PREPARACION DEL PEDIDO


[Subsistema Mostrador]
; -- Simulación - Trabajo  Práctico Grupo 10 | Tema: Venta de Piezas de Bronce --
; -- PEDIDOS EN MOSTRADOR --
		GENERATE	480, 300	; Arriban clientes cada 8 +/- 5 minutos.
		QUEUE		MOST
		SEIZE		MOST
		DEPART	MOST
		ADVANCE	120,60	; 2+-1 MINUTOS GENERAL
		ASSIGN	PRODUCTOS, FN$CANTPROD	; OBTENGO CANTIDAD DE PRODUCTOS AL AZAR
		ASSIGN	CANTPIEZ, FN$CANTPIEZ		; OBTENGO CANTIDAD DE PIEZAS AL AZAR
		ASSIGN	PRODAUX, P$PRODUCTOS			; AUXILIAR PARA LOOP
OTPROD1	ADVANCE	10,2		; 10+-2SEG POR PRODUCTO
		LOOP		PRODAUX, OTPROD1
		RELEASE	MOST
		TRANSFER	,PREPARAR	; IR A PREPARACION DEL PEDIDO


[Subsistema Parametrización]
; -- Simulación - Trabajo  Práctico Grupo 10 | Tema: Venta de Piezas de Bronce --
PREP		STORAGE	2		; 2 EMPLEADOS QUE PREPARAN EL PEDIDO
CANTPROD	FUNCTION	RN2,C4	; FUNCION DE CANTIDAD DE PRODUCTOS POR PEDIDO
0,1/.6,13/.95,21/1,101		; EL PRIMER TRAMO SALTEA LO MENOR A 1, Y COMO TRUNCA SUMO 1
CANTPRODTEL	FUNCTION		RN2,C2	; FUNCION DE CANTIDAD DE PRODUCTOS POR PEDIDO
0,1/1,11					; EL PRIMER TRAMO SALTEA LO MENOR A 1, Y COMO TRUNCA SUMO 1
CANTPIEZ	FUNCTION	RN3,C8	; FUNCION DE CANTIDAD DE PIEZAS POR PEDIDO
0,1/.14,21/.31,51/.51,201/.67,301/.82,501/.99,2001/1,15001
LOTEPIEZ	VARIABLE	1+P$CANTPIEZ/20	; ...POR CADA 20 PIEZAS...
TPREP		TABLE		MP2,8,400,10	; TIEMPO TOTAL PARA PREPARAR EL PEDIDO
QSINT15	TABLE		S$PREP,0,10,20	; CANTIDAD DE PEDIDOS SIN TERMINAR CADA 15 MINUTOS
QSINT1	TABLE		S$PREP,0,10,20	; CANTIDAD DE PEDIDOS SIN TERMINAR CUANDO SE TERMINA DE TOMAR UNO
QPEDTERM	TABLE		N$PREPARAR,10,50,20	; CANTIDAD DE PEDIDOS CUANDO SE TERMINA DE DESPACHAR UNO


[Subsistema cada 15min]
; -- Simulación - Trabajo  Práctico Grupo 10 | Tema: Venta de Piezas de Bronce --
		GENERATE	900		
		TABULATE	QSINT15	; TABULO CANTIDAD DE PEDIDOS SIN TERMINAR, CADA 15 MINUTOS
		TERMINATE


<\F>
Nombre=
Descripcion=
Version=
Autor=
Empresa=
Opciones=
