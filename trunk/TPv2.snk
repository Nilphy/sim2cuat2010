[Principal]
EMPLEADOS	STORAGE	2								;HAY 2 EMPLEADOS QUE SE ENCARGAN DE PREPARAR LOS PEDIDOS.

;FUNCIONES
TIPO_PROD	FUNCTION	RN2,E3							;SE DETERMINA QUE INTERVALO SE USARA PARA SABER LA CANTIDAD DE PRODUCTOS.
0.6,V$T_PROD_1/0.95,V$T_PROD_2/1,V$T_PROD_3

TIPO_PIEZA	FUNCTION	RN3,E7							;SE DETERMINA QUE INTERVARLO SE USARA PARA SABER LA CANTIDAD DE PIEZAS
0.14,V$T_PIEZA_1/0.31,V$T_PIEZA_2/0.51,V$T_PIEZA_3/0.67,V$T_PIEZA_4/0.82,V$T_PIEZA_5/0.99,V$T_PIEZA_6/1,V$T_PIEZA_7

;VARIABLES
;PARA CADA INTERVALO DE PIEZA SE DEFINE UNA VARIABLE.
;HIPOTESIS:
;SE SUPONE QUE LA CANTIDAD DE PIEZAS ES PARA EL TOTAL DE PRODUCTOS Y NO PARA CADA PRODUCTO.
T_PIEZA_1	VARIABLE	1+RN4@20							;1 A 20
T_PIEZA_2	VARIABLE	21+RN5@30							;21 A 50
T_PIEZA_3	VARIABLE	51+RN6@150							;50 A 200
T_PIEZA_4	VARIABLE	201+RN7@100						;201 A 300
T_PIEZA_5	VARIABLE	301+RN8@200						;301 A 500
T_PIEZA_6	VARIABLE	501+RN9#10@1500						;501 A 2000
T_PIEZA_7	VARIABLE	2001+RN10#1000@13000					;2001 A 15000

;PARA CADA INTERVALO DE PRODUCTO SE DEFINE UNA VARIABLE.
T_PROD_1	VARIABLE	1+RN11@12							;1 A 12
T_PROD_2	VARIABLE	13+RN11@8							;13 A 20
T_PROD_3	VARIABLE	21+RN11@80							;21 A 100

TI_PE_PI	VARIABLE	P$CANT_PIEZ/20+1					;TIEMPO PEDIDO PIEZAS: CADA 20 PIEZAS SE MULTIPLICA LA CANTIDAD DE TIEMPO QUE DEMORA EL PEDIDO.

;TABLAS
TIEMPO_PREPARAR_PEDIDO		QTABLE	EMPLEADOS,60,60,10
CANT_PEDIDOS_SIN_TERMINAR		TABLE		Q$EMPLEADOS,0,1,20
CANT_PEDIDOS_SIN_TERMINAR_UNO	TABLE		Q$EMPLEADOS,0,1,20	;CANTIDAD DE PEDIDOS SIN TERMINAR CUANDO SE TERMINA DE TOMAR UNO.
;HIPOTESIS:
;SE SUPONE QUE EL PEDIDO ES DESPACHADO CUANDO SE TERMINA DE FACTURAR
CANT_PEDIDOS_SIN_TERMINAR_DESP	TABLE		Q$EMPLEADOS,0,1,20	;CANTIDAD DE PEDIDOS SIN TERMINAR CUANDO SE TERMINA DE DESPACHAR UNO.




















[Tomar pedido por telefono]
				GENERATE	1200,600				;LLAMADOS TELEFONICO DE CLIENTES. (20 ± 10)MIN.
				QUEUE		TELEFONO	
				SEIZE		TELEFONO				;SOLO HAY UN VENDEDOR TELEFONICO.
				DEPART	TELEFONO	
				ADVANCE	180,60				;TIEMPO DE ATENCION POR TELEFONO, POR DEFECTO (3 ± 1)MIN.
				ASSIGN	CANT_PROD,FN$TIPO_PROD	;EL CLIENTE ELIGE CANT_PROD PRODUCTOS.
				ASSIGN	CANT_PIEZ,FN$TIPO_PIEZA	;EL CLIENTE ELIGE CANT_PIEZ PIEZAS.
				ASSIGN	AUX,P$CANT_PROD			;PARAMETRO AUXILIAR PARA EL LOOP DE PRODUCTOS.
SIG_PRODUCTO_TELEFONO	ADVANCE	7,2					
				LOOP		AUX,SIG_PRODUCTO_TELEFONO	;EL LOOP CONTINUA HASTA QUE LA CANTIDAD DE PRODUCTOS (CANT_PROD) ES 0.
				RELEASE	TELEFONO				;TERMINA LA LLAMADA.
				TABULATE	CANT_PEDIDOS_SIN_TERMINAR_UNO
				TRANSFER	,PREPARAR_PEDIDO		;HAY QUE PREPARAR EL PEDIDO
				

[Preparar pedido]
PREPARAR_PEDIDO		QUEUE		EMPLEADOS
				ENTER		EMPLEADOS
				DEPART	EMPLEADOS
				ADVANCE	300,120			;TIEMPO, POR DEFECTO, QUE DEMORA EL EMPLEADO EN PREPARAR EL PEDIDO (5 ± 2)MIN.
				ASSIGN	AUX_PROD,P$CANT_PROD	;PARAMETRO AUXILIAR PARA EL LOOP DE PRODUCTOS.
TIE_PED_PRO		ADVANCE	5,2
				LOOP		AUX_PROD,TIE_PED_PRO	
				ASSIGN	AUX_PIEZ,V$TI_PE_PI	;PARAMETRO AUXILIAR PARA EL LOOP DE PIEZAS.
SEGUIR_PREPARANDO	ADVANCE	3,1
				LOOP		AUX_PIEZ,SEGUIR_PREPARANDO
				LEAVE		EMPLEADOS
				TRANSFER	,FACTURAR_PEDIDO	;HAY QUE FACTURAR EL PEDIDO.


[Facturar pedido]
FACTURAR_PEDIDO		QUEUE		FACTURAR
				SEIZE		FACTURAR
				DEPART	FACTURAR
				ADVANCE	120,30		;TIEMPO, POR DEFECTO, QUE DEMORA LA FACTURACION.
				ASSIGN	AUX,P$CANT_PROD	;PARAMETRO AUXILIAR PARA EL LOOP.
SEGUIR_FACTURANDO	ADVANCE	5,2
				LOOP		AUX,SEGUIR_FACTURANDO
				RELEASE	FACTURAR
				TABULATE	CANT_PEDIDOS_SIN_TERMINAR_DESP
				TERMINATE			


[Jornada laboral]
GENERATE	3600
TERMINATE	1

[Tomar pedido por mostrador]
					GENERATE	480,300				;ARRIBO DE CLIENTES. (8 ± 5)MIN.
					QUEUE		TELEFONO	
					SEIZE		TELEFONO				;SOLO HAY UN VENDEDOR TELEFONICO.
					DEPART	TELEFONO	
					ADVANCE	120,60				;TIEMPO DE ATENCION POR MOSTRADOR, POR DEFECTO (3 ± 1)MIN.
					ASSIGN	CANT_PROD,FN$TIPO_PROD	;EL CLIENTE ELIGE CANT_PROD PRODUCTOS.
					ASSIGN	CANT_PIEZ,FN$TIPO_PIEZA	;EL CLIENTE ELIGE CANT_PIEZ PIEZAS.
					ASSIGN	AUX,P$CANT_PROD			;PARAMETRO AUXILIAR PARA EL LOOP DE PRODUCTOS.
SIG_PRODUCTO_MOSTRADOR	ADVANCE	10,2					
					LOOP		AUX,SIG_PRODUCTO_MOSTRADOR	;EL LOOP CONTINUA HASTA QUE LA CANTIDAD DE PRODUCTOS (CANT_PROD) ES 0.
					RELEASE	TELEFONO				;TERMINA LA LLAMADA.
					TABULATE	CANT_PEDIDOS_SIN_TERMINAR_UNO
					TRANSFER	,PREPARAR_PEDIDO		;HAY QUE PREPARAR EL PEDIDO
					

[Tabular cantidad de pedidos sin terminar]
GENERATE	1800
TABULATE	CANT_PEDIDOS_SIN_TERMINAR
TERMINATE

<\F>
Nombre=
Descripcion=
Version=
Autor=
Empresa=
Opciones=
